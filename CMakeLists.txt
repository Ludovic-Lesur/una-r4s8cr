#
# CMakeLists.txt
#
#  Created on: 14 dec. 2025
#      Author: Ludo
#

# Minimum CMake version.
cmake_minimum_required(VERSION 3.23)

# Project creation.
project(una-r4s8cr)

# Build mode.
if(NOT DEFINED BUILD_MODE)
    set(BUILD_MODE "STATIC")
endif()

# Create library.
add_library(${PROJECT_NAME} ${BUILD_MODE})

# Static library.
if(${BUILD_MODE} STREQUAL STATIC)

    # Macro to convert options to variables.
    macro(add_compilation_flag FLAG_NAME FLAG_DESCRIPTION FLAG_DEFAULT_VALUE)
        option(FLAG_NAME FLAG_DESCRIPTION FLAG_DEFAULT_VALUE)
        set(${FLAG_NAME} ${FLAG_DEFAULT_VALUE} CACHE STRING ${FLAG_DESCRIPTION})
        list(APPEND COMPILATION_FLAGS_LIST ${FLAG_NAME})
    endmacro()
    
    # Compilation flags.
    add_compilation_flag(UNA_R4S8CR_DISABLE "Disable the UNA R4S8CR functions." OFF)
    add_compilation_flag(UNA_R4S8CR_NODE_ACCESS_RETRY_MAX "Number of relay box access retries in case of failure." 3)
    add_compilation_flag(UNA_R4S8CR_USE_REGISTER_ACCESS "Enable register access definitions." ON)
    add_compilation_flag(UNA_R4S8CR_USE_REGISTER_ACCESS_TIMEOUT "Enable register access timeout definitions." ON)
    add_compilation_flag(UNA_R4S8CR_USE_REGISTER_ERROR_VALUE "Enable register error values definitions." ON)
    
    # Remove OFF flags from list and keep flags set to value 0.
    foreach(FLAG ${COMPILATION_FLAGS_LIST})
        if((NOT ${FLAG} STREQUAL OFF) AND (NOT ${FLAG} STREQUAL ON))
            set(${FLAG} "(${${FLAG}})")
        endif()
    endforeach()   
    
    # Create flags file.
    configure_file(una_r4s8cr_flags.h.in una_r4s8cr_flags.h @ONLY)
    
    # Fixed compilation flags of dependencies.
    if(${BUILD_MODE} STREQUAL STATIC)
        target_compile_definitions(${PROJECT_NAME}
            PRIVATE
                EMBEDDED_UTILS_DISABLE_FLAGS_FILE
                R4S8CR_DRIVER_DISABLE_FLAGS_FILE
                R4S8CR_DRIVER_DELAY_ERROR_BASE_LAST=${R4S8CR_DRIVER_DELAY_ERROR_BASE_LAST}
                R4S8CR_DRIVER_RS485_ERROR_BASE_LAST=${R4S8CR_DRIVER_RS485_ERROR_BASE_LAST}
                UNA_LIB_DISABLE_FLAGS_FILE
        )
    endif()
    
    # Dependencies folders.
    target_include_directories(${PROJECT_NAME}
        PUBLIC
            ${CMAKE_CURRENT_BINARY_DIR}
            ${TYPES_PATH}
            ${EMBEDDED_UTILS_PATH}/inc
            ${R4S8CR_DRIVER_PATH}/inc
            ${UNA_LIB_PATH}/inc
    )
    
    # Print archive size.
    add_custom_command(TARGET ${PROJECT_NAME} POST_BUILD 
        COMMAND ${CMAKE_SIZE_UTIL} -t lib${PROJECT_NAME}.a
    )
    
endif()

# Source files list.
target_sources(${PROJECT_NAME}
    PRIVATE
        ${CMAKE_CURRENT_SOURCE_DIR}/src/r4s8cr_registers.c
        ${CMAKE_CURRENT_SOURCE_DIR}/src/una_r4s8cr.c
)

# Header files folder.
target_include_directories(${PROJECT_NAME}
    PUBLIC
        ${CMAKE_CURRENT_SOURCE_DIR}/inc
)